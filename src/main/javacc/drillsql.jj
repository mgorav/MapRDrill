
options {
    LOOKAHEAD = 1;
    FORCE_LA_CHECK = true;
    CHOICE_AMBIGUITY_CHECK = 3;
    OTHER_AMBIGUITY_CHECK = 2;
    STATIC = false;
    DEBUG_PARSER = false;
    DEBUG_LOOKAHEAD = false;
    DEBUG_TOKEN_MANAGER = false;
    UNICODE_INPUT = true;
    SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
}

PARSER_BEGIN(BaseParser)

package com.gm.drill.sql.parser;

import java.io.InputStream;
import java.util.ArrayList;
import java.util.List;

final class BaseParser {

    private ExpressionFactory factory;

    public BaseParser(InputStream stream, String encoding, ExpressionFactory factory) {
        this(stream, encoding);
        this.factory = factory;
    }
}

PARSER_END(BaseParser)


SKIP : {
    " " | "\t"
}


TOKEN : {
    < #ALPHANUM : (["a"-"z", "A"-"Z", "0"-"9","_"])+ >
}

TOKEN : {
    < #ATTR : (<ALPHANUM> ("(+)")?) >
}

TOKEN : {
   < #OPERA      : ( "eq" | "eqic" | "neq" | "lt" | "lte" | "gt" | "gte" | "in" | "out" | "bet" | "like" | "likeic" | "isNull" | "isNotNull") >
}

TOKEN : {
    < ATTROPER    : ( <ATTR> ( "." <ATTR> )* ("." <OPERA> ) ) >
}




TOKEN : {
   < LPARA     : "(" >
  | < RPARA     : ")" >
}

TOKEN : {   
    < UNRESERVED_STR    : ( ~["\"", "'", "(", ")", ";", ",", "=", "<", ">", "!", "~", " ", "."] )+ > 
  |	< SINGLE_QUOTED_STR : ( "'" ( ~["'"] )* "'" ) > {
        matchedToken.image = image.substring(1, image.length() -1);
    }
  | < DOUBLE_QUOTED_STR : ( "\"" ( ~["\""] )* "\"" ) > {
        matchedToken.image = image.substring(1, image.length() -1);
    }
}

TOKEN : {
    < AND        : ".and." >
  | < OR         : ".or."  >
}

Expression Input():
{
    final Expression queryExpression;
}
{
    queryExpression = Or() <EOF>
    {
        return queryExpression;
    }
}

Expression Or():
{
    final List<Expression> queryExpressions = new ArrayList<Expression>(3);
    Expression queryExpression;
}
{
    queryExpression = And() { queryExpressions.add(queryExpression); }
    (
       <OR> queryExpression = And() { queryExpressions.add(queryExpression); }
    )*
    {
        return queryExpressions.size() != 1 ? factory.createLogicalExpression(LogicalOperator.OR, queryExpressions) : queryExpressions.get(0);
    }
}

Expression And():
{
    final List<Expression> queryExpressions = new ArrayList<Expression>(3);
    Expression queryExpression;
}
{
    queryExpression = Constraint() { queryExpressions.add(queryExpression); }
    (
       <AND> queryExpression = Constraint() { queryExpressions.add(queryExpression); }
    )*
    {
        return queryExpressions.size() != 1 ? factory.createLogicalExpression(LogicalOperator.AND, queryExpressions) : queryExpressions.get(0);
    }
}

Expression Constraint():
{
    final Expression queryExpression;
}
{
    ( queryExpression = Group() | queryExpression = Comparison() )
    {
        return queryExpression;
    }
}

Expression Group():
{
    final Expression queryExpression;
}
{
    <LPARA> queryExpression = Or() <RPARA>
    {
        return factory.createGroupExpression(queryExpression);
    }
}

ComparisonExpression Comparison():
{
    final String attrAndOper;
    final List<String> args;
}
{
    ( attrAndOper = AttrAndOper() args = Arguments() )
    {
        return factory.createComparisonExpression(attrAndOper, args);
    }
}

String AttrAndOper(): {}
{
    token = <ATTROPER>
    
    {
        return token.image;
    }
}

List<String> Arguments():
{
    final List<String> value;
}
{
    ( <LPARA> value = CommaSepArguments() <RPARA> ) { return value; }
}

List<String> CommaSepArguments():
{
    final List<String> list = new ArrayList<String>(30);
    String arg;
}
{
    arg = Argument() { list.add(arg); }
	    (
	        <",">
	        arg = Argument() { list.add(arg); }
	    )*
	{
        return list;
    }
}

String Argument(): {}
{
    ( token = <UNRESERVED_STR> | token = <DOUBLE_QUOTED_STR> | token = <SINGLE_QUOTED_STR>)
    {
        return token.image;
    }
}
